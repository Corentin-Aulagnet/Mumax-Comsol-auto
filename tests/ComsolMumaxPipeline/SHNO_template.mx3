Nx := $cell_number
Ny := $cell_number
Nz := 1

CellX := $cell_size
CellY := $cell_size
CellZ := 4.96E-9
SetCellsize(CellX, CellY, CellZ)
SetGridsize(Nx, Ny, Nz)

setgeom(imageShape("${geom_file}"))
defregion(1, rect(50e-9, 50e-9))
setPBC(3, 3, 0)
edgesmooth = 8

//Magnetic parameters
Msat = 5.89e5
Aex = 12e-12
alpha = 0.02
m = uniform(0, 1, 0)

//1st axis
K1 := 2.5e5
u1 := ConstVector(0,1,0)
prefactor1 := Const(2*K1/Msat.Average())
customAnisField1 := Mul(prefactor1,Mul(Dot(m,u1),u1))
AddFieldTerm(customAnisField1)
AddEdensTerm(Mul(Const(-K1),Mul(Dot(m,u1),Dot(m,u1))))

//2nd axis
K2 := 7.5e5
u2 := ConstVector(0,0,1)
prefactor2 := Const(2*K2/Msat.Average())
customAnisField2 := Mul(prefactor2,Mul(Dot(m,u2),u2))
AddFieldTerm(customAnisField2)
AddEdensTerm(Mul(Const(-K2),Mul(Dot(m,u2),Dot(m,u2))))


//SOT parameters
SOTxi := -2
alphaH := 0.07
Pol = alphaH
Lambda = 1
EpsilonPrime = alphaH / 2 * SOTxi
GammaLL = 29.53e9
fixedlayer = vector(-1, 0, 0)

//Bias field
fieldMag := 0.04
inPlaneAngle := 10 * pi / 180 //away from x-axis
outOfPlaneAngle := 80 * pi / 180 //away from z-axis 
B_ext = vector(fieldMag*cos(inPlaneAngle)*sin(outOfPlaneAngle), fieldMag*sin(inPlaneAngle)*sin(outOfPlaneAngle), fieldMag*cos(outOfPlaneAngle))
B_ext.add(LoadFile("${oersted_file}"), 1)
J.Add(LoadFile("${J_file}"), 1)

//autosave(m.Region(1),2e-10)
tableadd(m.Region(1))
tableautosave(10e-12)
run(100e-9)